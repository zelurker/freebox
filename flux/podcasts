#!/usr/bin/perl

use strict;
use Date::Parse;
# xml::simple est vraiment obligatoire ici, format xml trop variable
use XML::Simple;
use Data::Dumper;
use WWW::Mechanize;
use HTML::Entities;
use link_get;
use v5.10;

# Refonte complète, on abandonne podcasters qui est pourri finalement
# et on ne va garder que des résultats google pour les recherches...
#
# C'est un peu long pour un plugin mais c'est + simple que celui de youtube
# (jusqu'ici en tous cas !). En gros :
# 1ère partie : détection du type de requête à faire, exactement comme
# youtube, mais faire un module pour ça ne serait pas très pratique
# 2 - décodage du xml des podcasts avec xml::simple

our $latin = ($ENV{LANG} !~ /UTF/i);
# if (!$latin) {
# 	binmode(STDOUT, ":utf8");
# 	binmode(STDERR, ":utf8");
# }
my $f;
my @abo;
my $fpod = "$ENV{HOME}/.freebox/podcasts";
if (open($f,"<$fpod")) {
	@abo = <$f>;
	close($f);
	chomp @abo;
}
my $str = shift @ARGV;
if ($str eq "del") {
	my $url = shift @ARGV;
	for (my $n=1; $n <= $#abo; $n++) {
		if ($abo[$n] eq $url) {
			splice @abo,$n-1,2;
			save_pods();
			last;
		}
	}
	exit(0);
}
my $debug = 0; # infos
my $dump = 0;  # dump toute l'entrée ligne par ligne
$str =~ s/^result\://;
my $mech = WWW::Mechanize->new();
$mech->agent_alias("Linux Mozilla");
$mech->timeout(10);
$mech->default_header('Accept-Encoding' => scalar HTTP::Message::decodable());

if ($str =~ /^(.+) (http.+)/) { # demande d'abonnement
	my $title = $1;
	$str = $2;
	push @abo,$title;
	push @abo,$2;
	save_pods();
	exit(0);
} elsif ($str eq "Abonnements") {
	header("list");
	print join("\n",@abo);
	exit(0);
} elsif ($str =~ s/^get,(\d+):(.+)/$2/) {
	# Lien download
	my $size = $1;
	mkdir "podcasts";
	my ($file) = $str =~ /.+\/(.+)/;
	$file =~ s/\?.+//; # Vire tout ce qui suit d'éventuels params !
	$file = "podcasts/$file";

	if ($latin) {
		open(F,"<:encoding(iso-8859-1)","pod") || die "peut pas lire pod\n";
	} else {
		open(F,"<:encoding(utf8)","pod") || die "peut pas lire pod\n";
	}
	# open(F,"<","pod") || die "peut pas lire pod\n";
	@_ = <F>;
	close(F);
	$_ = join("\n",@_);
	my $ref = XMLin($_);
	my $item = $ref->{channel}->{item};
	my $found = 0;
	foreach (@$item) {
		if ($_->{enclosure}->{url} eq $str) {
			print STDERR "génération $file.info\n";
			$found = 1;
			open(F,">$file.info");
			my $img = $ref->{"channel"}->{image}->{url};
			print F ($img ? "pic:$img " : ""),$_->{title},"\n",$_->{"itunes:subtitle"},"\n";
			print F $_->{description},"\n";
			close(F);
			last;
		}
	}
	if (!$found) {
		print STDERR "problem, could not find url in pod file !\n";
	}
	link_get::link_get($file,$str,$size);
}
$str =~ s/ /\+/g;
my ($r,$type);
if ($debug && !$str) {
	@_ = <>;
	$_ = join("",@_);
	$str = "http";
	print STDERR "debug mode, input from stdin\n";
} else {
	if ($str =~ /^prog.:(.+)/) {
		$mech->get($1);
	} elsif ($str && $str !~ /^http/) {
		print STDERR "requête de recherche sur $str\n"; # if ($debug);
		$mech->get("https://www.google.fr/");
		$r = $mech->submit_form(
			form_number => 1,
			fields      => {
				# Note qu'on peut ajouter filetype:xml pour avoir les
				# résultats xml de google, sauf que certains sites genre
				# radiofrance utilisent un robots.txt pour exclure ces
				# pages des résultats de recherche !
				q => "podcast $str",
			}
		);
	} elsif ($str) {
		print STDERR "debug: get $str\n" if ($debug);
		$r = $mech->get($str);
	} elsif (!$str) {
		header("list");
		exit(0);
	}
	$_ = $mech->content(charset => "utf8");
	# Apparemment l'apostrophe windows n'est même pas de l'utf8 standard
	# ça provoque un sacré merdier, alors on traque les codes étendus ici...
	if ($latin) {
		for (my $n=1; $n<=length($_); $n++) {
			my $code = ord(substr($_,$n,1));
			if ($code > 255) {
				print STDERR "replace code $code\n";
				if ($code == 8217) {
					substr($_,$n,1) = "'";
				} else {
					substr($_,$n,1) = ".";
				}
			}
		}
	}
	$type = $r->header("content-type") if ($r);
}

my $encoding = "";

my $content = 0;
my ($link,$title,$desc,$img,$date);
print STDERR "*** type $type\n";
if (/^<\?xml/ || /^<rss xml/ || $type =~ /(application|text).xml/) {
	header("direct");
	my $char = chr(8217); # apostrophe ?
	s/$char/\'/g;
	$char = chr(8230); # apostrophe fermé apparemment, merdier windoze
	s/$char/\'/g;
	# Ne surtout pas appeler ici decode_entities, le xml est très strict
	# là-dessus
	if ($r && open(F,">","pod")) {
		print F $_;
		close(F);
	}
	# A priori c'est pas la peine de repasser le & ici...
	# s/&([^a])/&amp;$1/g;
	print STDERR "décodage xml\n" if ($debug);
	# fichier xml podcast
	my $found = 0;
	foreach (@abo) {
		if ($_ eq $str) {
			$found = 1;
			last;
		}
	}
	print STDERR "trouvé abonnement\n" if ($found && $debug);

	s/\r//gm;
	my $ref = XMLin($_);
	if (!$found) {
		my $title = decode_entities($ref->{channel}->{title});
		print "S'abonner\n$title $str\n";
	}

	my $item = $ref->{channel}->{item};
	# Vu que certains podcasts n'arrivent pas dans l'ordre, trie par date
	my @item = sort
	{
		my $c = $a->{pubDate}; my $d = $b->{pubDate};
		# print "avant $c et $d\n";
		if ($c !~ /^\d+$/) {
			$c =~ s/Mars/Mar/;
			$d =~ s/Mars/Mar/;
			$c = str2time($c);
			$d = str2time($d);
		}
		# print "tri $c et $d\n";
		$d <=> $c;
	} @$item;
	binmode STDOUT;
	foreach (@item) {
		# A priori ça ne semble pas être une bonne idée de coller l'image ici
		# j'ai jamais vu ces images changer pour chaque épisode et généralement
		# elles sont assez grosses donc elles seront mieux dans le bandeau
		# d'info. Je laisse quand même le code en commentaire au cas où...
		# my $img = $_->{"media:thumbnail"}->{url};
		# print "pic:$img " if ($img && $nb++ <= 10);
		print decode_entities($_->{title});
		my $date = $_->{pubDate};
		$date = str2time($date) if ($date !~ /^\d+$/);
		print " le ".get_date($date) if ($date);
		print "\nget,",$_->{enclosure}->{length},":",$_->{enclosure}->{url},"\n";
	}
	exit(0);
}

# Ici on interprête le résultat de la recherche...
header("list");
my $u = $mech->uri;
if ($u =~ /google\./) {
	foreach ($mech->links) {
		my $u = $_->url;
		if ($u =~ /url.q=(http.+?)&/) {
			$u = $1;
			print $_->text,"\n$u\n";
		}
	}
} else {
	my ($host) = $u =~ /https?:\/\/(.+?)\//;
	print STDERR "host for filtering $host\n";
	foreach ($mech->links) {
		my $u = $_->url;
		next if ($u =~ /$host/);
		next if ($u !~ /^http/ || $u !~ /xml$/);
		print $_->text,"\n$u\n";
	}
}

sub get_date {
	my $time = shift;
	my ($sec,$min,$hour,$mday,$mon,$year) = localtime($time);
	sprintf("%d/%02d/%02d, %02d:%02d",$mday,$mon+1,$year+1900,$hour,$min);
}

sub header {
	my $msg = shift;
	say "encoding: latin1";
	print "$msg\n";
	print "Recherche\nRecherche\n"; # Transmis en mode list (ne contient pas //)
	print "Consulter une url xml...\nRecherche:url podcast xml\n"; # Transmis en mode list (ne contient pas //)
	print "Abonnements\n+Abonnements\n" if (@abo);
}

sub save_pods {
	if (open($f,">$fpod")) {
		print $f join("\n",@abo);
		close($f);
	}
}
